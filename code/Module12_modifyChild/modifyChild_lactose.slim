initialize() {
	//initializeMutationRate(1e-7);
	initializeMutationRate(0);
	initializeMutationType("m1", 0.5, "f", 0.0);
	initializeMutationType("m2", 0.5, "f", 0.1);
	m2.convertToSubstitution = F;
	initializeGenomicElementType("g1", c(m1,m2), c(0.99,0.01));
	initializeGenomicElement(g1, 0, 99999);
	initializeRecombinationRate(1e-8);
	}

// create a population of 500 individuals
1 {
	sim.addSubpop("p1", 1000);
	// start as mostly non-milk-drinkers
	p1.individuals.tag = rbinom(1000, 1, 0.01);
}

modifyChild() {
	// inherit culture from parents, with some deviation
	parentCulture = (parent1.tag + parent2.tag) / 2;
	childCulture = rbinom(1, 1, 0.01 + 0.98 * parentCulture); child.tag = childCulture;
	return T;
}

fitness(m2) {
	// deleterious for non-milk-drinkers, beneficial for milk-drinkers
	return (individual.tag == 0) ? 0.95 else relFitness;
}


late() {
	// dark blue for non-milk-drinkers, light gray for milk-drinkers
	inds = p1.individuals;
	inds.color = ifelse(inds.tag == 0, "darkblue", "lightgray");
	catn(sim.generation + ": " + mean(inds.tag));
}


10000 { sim.simulationFinished(); }